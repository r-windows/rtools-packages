# Maintainer: Jeroen Ooms <jeroen@berkeley.edu>

_realname=udunits
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.28
pkgrel=1
pkgdesc="Unidata unit conversion library (mingw-w64)"
arch=('any')
license=("custom")
url="https://www.unidata.ucar.edu/software/udunits/"
source=("https://artifacts.unidata.ucar.edu/repository/downloads-udunits/${pkgver}/udunits-${pkgver}.tar.gz")
sha256sums=('590baec83161a3fd62c00efa66f6113cec8a7c461e3f61a5182167e0cc5d579e')
depends=("${MINGW_PACKAGE_PREFIX}-expat")
options=('staticlibs' 'strip')

build() {
  [[ -d "${srcdir}"/build-${CARCH}-static ]] && rm -rf "${srcdir}"/build-${CARCH}-static
  mkdir -p "${srcdir}"/build-${CARCH}-static && cd "${srcdir}"/build-${CARCH}-static

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}-static
  make install DESTDIR="${pkgdir}"
}
