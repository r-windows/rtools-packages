# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libpq
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=13.2
pkgrel=1
pkgdesc="Libraries for use with PostgreSQL (mingw-w64)"
arch=('any')
url="https://www.postgresql.org/"
license=('custom:PostgreSQL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2"
        only-static2.patch)
sha256sums=('5fd7fcd08db86f5b2aed28fcfaf9ae0aca8e9428561ac547764c2a2b0f41adfc'
            'SKIP')

prepare() {

  cd ${srcdir}/postgresql-${pkgver}
  patch -p1 --fuzz=10 -i ${srcdir}/only-static2.patch
  
  # Broken autoconf test for openssl
  if [ "${MINGW_PREFIX}" = "/mingw32" ]; then
  sed -i.bak 's/ -Wl,--disable-auto-import//g' src/template/win32
  fi
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
  LIBS="-lsecur32 -lws2_32 -lgdi32 -lcrypt32 -lwldap32" \
  ../postgresql-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --with-openssl \
    --with-ldap \
    --disable-thread-safety \
    --enable-integer-datetimes \
    --disable-nls \
    --disable-rpath \
    --without-libxml \
    --without-libxslt \
    --without-python \
    --without-perl \
    --without-tcl

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/"{bin,include,lib}

  make DESTDIR="${pkgdir}" -C "src/interfaces" install
  make DESTDIR="${pkgdir}" -C "src/common" install
  make DESTDIR="${pkgdir}" -C "src/port" install
  make DESTDIR="${pkgdir}" -C "src/include" install

  # Move dll's to bin directory
  # mv "${pkgdir}${MINGW_PREFIX}/lib/"*.dll "${pkgdir}${MINGW_PREFIX}/bin/"

  # these headers are needed by the not-so-public headers of the interfaces
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/include/{libpq,postgresql/internal/libpq}
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/c.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/"
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/port.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/"
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/postgres_fe.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/"
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/libpq/pqcomm.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/libpq/"

  # Fix pkg-config file
  sed -i 's/-lpq/-lpq -lpgport -lpgcommon/g' "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libpq.pc"
}
