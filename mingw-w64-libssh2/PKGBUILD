# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libssh2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.11.0
pkgrel=9800
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (mingw-w64)"
arch=('any')
url="https://libssh2.org"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
              "autoconf" "automake" "libtool")
depends=("${MINGW_PACKAGE_PREFIX}-openssl" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://libssh2.org/download/${_realname}-${pkgver}.tar.gz"{,.asc}
        fix-pkgconfig.patch)
sha256sums=('3736161e41e2693324deb38c26cfdc3efe6209d634ba4258db1cecff6a5ad461'
            'SKIP'
            '7a6545f6d457ad008aacefe04a60727c02d33927c8a903745bf191f69cc8ba55')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/fix-pkgconfig.patch
  autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  # For R 4.2 and up we switch to native Windows SSL
  #if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]] || [[ $MINGW_PACKAGE_PREFIX == *-ucrt-* ]]; then
  #  local _SSL_BACKEND=wincng
  #else
    local _SSL_BACKEND=openssl
    export LIBS="$(pkg-config --libs-only-l --static libssl)"
  #fi

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-examples-build \
    --enable-static \
    --disable-shared \
    --with-crypto=$_SSL_BACKEND

  if grep 'AES-CTR: no' config.log; then
    cat config.log
    echo "==========="
    echo "Failed to properly detect OpenSSL"
    exit 1
  fi

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
