# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libssh2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.10.1
pkgrel=9800
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (mingw-w64)"
arch=('any')
url="https://libssh2.org"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
              "autoconf" "automake" "libtool")
depends=("${MINGW_PACKAGE_PREFIX}-openssl" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')

# Build from git for now: https://github.com/libssh2/libssh2/issues/536#issuecomment-892897873
source=("${_realname}-${pkgver}"::"git+https://github.com/libssh2/${_realname}.git"
        "fix-pkgconfig.patch"
        "https://github.com/libssh2/libssh2/commit/b952674f.diff"
        "https://github.com/libssh2/libssh2/commit/17c9c1fc.diff")
sha256sums=('SKIP'
            '7a6545f6d457ad008aacefe04a60727c02d33927c8a903745bf191f69cc8ba55'
            'SKIP'
            'SKIP')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/fix-pkgconfig.patch
  #patch --reverse -p1 -i ${srcdir}/b952674f.diff
  #patch --reverse -p1 -i ${srcdir}/17c9c1fc.diff
  autoreconf -fi
  sed -i 's/libssl libcrypto/libssl libcrypto libcrypt32 libgdi32/g' configure
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  # For R 4.2 and up we switch to native Windows SSL
  #if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]] || [[ $MINGW_PACKAGE_PREFIX == *-ucrt-* ]]; then
  #  local _SSL_BACKEND=wincng
  #else
    local _SSL_BACKEND=openssl
    #export LIBS="-lcrypt32 -lgdi32"
  #fi

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --disable-shared \
    --with-crypto=$_SSL_BACKEND

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
