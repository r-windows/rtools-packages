# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=leptonica
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.83.1
pkgrel=1
pkgdesc="Leptonica library (mingw-w64)"
arch=('any')
url="http://www.leptonica.org"
license=("Leptonica")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=(${MINGW_PACKAGE_PREFIX}-giflib
         ${MINGW_PACKAGE_PREFIX}-libtiff
         ${MINGW_PACKAGE_PREFIX}-libpng
         ${MINGW_PACKAGE_PREFIX}-libwebp
         ${MINGW_PACKAGE_PREFIX}-openjpeg2
         ${MINGW_PACKAGE_PREFIX}-zlib)
options=('!libtool' 'strip')
source=("https://github.com/DanBloomberg/leptonica/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('8f18615e0743af7df7f50985c730dfcf0c93548073d1f56621e4156a8b54d3dd')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Fix pkg-config... remove this hack for next release
  sed -i.bak 's/@JPEG_LIBS@/@JPEG_LIBS@ @LIBJP2K_LIBS@/g' lept.pc.in
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
  CFLAGS+=" -DMINIMUM_SEVERITY=L_SEVERITY_WARNING -DOPJ_STATIC"
  ../${_realname}-${pkgver}/configure \
    --disable-dependency-tracking \
    --disable-silent-rules \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --disable-shared
  make
}

#check() {
#  cd "${srcdir}/build-${CARCH}"
#  make -j1 check
#}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install

  # Fix .pc file
  sed -s "s|$(cygpath -m ${MINGW_PREFIX})|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lept.pc  
}
