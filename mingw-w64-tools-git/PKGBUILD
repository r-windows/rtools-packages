# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Rtools: Jeroen Ooms <jeroenooms@gmail.com>

_realname=tools
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-svn")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.0.4
pkgrel=9002
pkgdesc="MinGW-w64 tools"
arch=('any')
url="https://mingw-w64.sourceforge.io/"
license=('custom')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-libmangle")
options=('staticlibs' '!emptydirs')
source=("https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2"
        gcc8.patch)
sha256sums=('5527e1f6496841e2bb72f97a184fc79affdcd37972eaa9ebf7a5fd05c31ff803'
            'SKIP')
_tools="gendef genlib genidl genpeimg widl" # genstubdll

prepare() {
  cd ${srcdir}/mingw-w64-v${pkgver}
  #git am "${srcdir}/0001-widl-Relocate-DEFAULT_INCLUDE_DIR.patch"
  patch -p1 -i ${srcdir}/gcc8.patch
}

build() {
  local cur=
  for cur in ${_tools}; do
    plain "Building ${cur} ..."
    [[ -d ${srcdir}/${MINGW_CHOST}-${cur} ]] && rm -rf ${srcdir}/${MINGW_CHOST}-${cur}
    mkdir -p ${srcdir}/${MINGW_CHOST}-${cur}
    cd ${srcdir}/${MINGW_CHOST}-${cur}
    ${srcdir}/mingw-w64-v${pkgver}/mingw-w64-tools/${cur}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --with-mangle=${MINGW_PREFIX}
    make
  done
}

package() {
  local cur=
  for cur in ${_tools}; do
    plain "Installing ${cur} ..."
    cd ${srcdir}/${MINGW_CHOST}-${cur}
    make DESTDIR=${pkgdir} install
    if [[ -f "${srcdir}/mingw-w64/mingw-w64-tools/${cur}"/COPYING ]]; then
      install -Dm644 ${srcdir}/mingw-w64/mingw-w64-tools/${cur}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/mingw-w64-tools/COPYING.${cur}
    fi
  done
}
