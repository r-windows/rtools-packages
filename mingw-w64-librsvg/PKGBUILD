# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=librsvg
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.56.3
pkgrel=1
pkgdesc="SVG rendering library (mingw-w64)"
arch=('any')
url="https://wiki.gnome.org/Projects/LibRsvg"
license=("spdx:LGPL-2.1-or-later")
makedepends=("autoconf" "automake" "libtool"
             "${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-harfbuzz"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
options=('staticlibs' 'strip')
source=("https://download.gnome.org/sources/librsvg/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "0001-configure-set-pass-prefix-to-pkg-config-when-retriev.patch"
        "glib-0.17.10.tar.gz::https://crates.io/api/v1/crates/glib/0.17.10/download"
        "glib-rs.patch")
sha256sums=('5a328048a02d014645cd27f61140f4e0b11280fb2c7f2a21864fe0c59ac1ce88'
            '189eec6486c9ef6cf1071af9750405bc41e16ae3a55ad663435e574369fec915'
            'SKIP'
            'SKIP')
msys2_repository_url="https://gitlab.gnome.org/GNOME/librsvg"

prepare() {
  # Patch the glib rust bindings first
  cd "${srcdir}/glib-0.17.10"
  patch -p1 -i "${srcdir}/glib-rs.patch"

  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-configure-set-pass-prefix-to-pkg-config-when-retriev.patch"

  # Hack: use external rust which lets us select a target version
  export PATH="$(cygpath -u $USERPROFILE)/.cargo/bin:$PATH"
  TARGET="${MINGW_CHOST/w64-mingw32/pc-windows-gnu}"

  # Avoid the ntdll dependency which gives conflicts on msvcrt
  if command -v rustup; then
    echo "Found existing rustup!"
    rustup target add $TARGET
    rustup default "1.69-$TARGET"
  else
    curl -L https://win.rustup.rs -o rustup-init.exe
    ./rustup-init.exe -yv --default-toolchain stable --default-host $TARGET
  fi

  echo "PATH=$PATH"
  rustc --version

  ## Jeroen: try to fix static linking of rust to glib
  ## See: https://github.com/msys2/MINGW-packages/issues/18574
  echo "" >> Cargo.toml
  echo "[patch.crates-io]" >> Cargo.toml
  echo "glib = { path = '../glib-0.17.10' }" >> Cargo.toml

  cargo update --package glib
  cargo fetch --locked
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  # html documentation doesn't install from out of tree
  cp -rf "${srcdir}/${_realname}-${pkgver}/doc" "${srcdir}/build-${MSYSTEM}/doc"

  # https://github.com/msys2/MINGW-packages/pull/13622#issuecomment-1285820970
  export WINAPI_NO_BUNDLED_LIBRARIES=1

  export lt_cv_deplibs_check_method='pass_all'

  # an argument passed to glib-mkenums starts with a c++ comment
  export MSYS2_ARG_CONV_EXCL="/*"
  # so the unix path gets preserved when passed through pkg-config
  MSYS2_ARG_CONV_EXCL="$MSYS2_ARG_CONV_EXCL;--define-variable=prefix"

  # Autoconf thinks we need pangoft2, but it is not needed.
  cp -fv ${MINGW_PREFIX}/lib/pkgconfig/pango.pc ${MINGW_PREFIX}/lib/pkgconfig/pangoft2.pc

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --disable-shared \
    --disable-introspection \
    --enable-tools=yes \
    --enable-pixbuf-loader=yes \
    --enable-gtk-doc=no \
    --enable-gtk-doc-html=no \
    --enable-silent-rules

  make
}

package() {
  make -C "${srcdir}/build-${MSYSTEM}" DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LIB" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB"
}
