_realname=poppler
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.72.0
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0 (mingw-w64)"
arch=('any')
url="https://poppler.freedesktop.org/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-poppler-data"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('strip' 'staticlibs')
source=("https://poppler.freedesktop.org/${_realname}-${pkgver}.tar.xz"
        004-basename-include.patch
        005-openjpeg2-include-dirs.patch
        static-mingw.patch
	backports.patch)
sha256sums=('c1747eb8f26e9e753c4001ed951db2896edc1021b6d0f547a0bd2a27c30ada51'
            'a0d1dfb3d5e698cc8d3348b108ede52d44165122e57754c6a696efe745e93030'
            'c2e401f94739a15125c06041eef2e4f988355422aa35aaea2144f7dc7bdfa047'
            'SKIP'
            'SKIP')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/004-basename-include.patch
  patch -p1 -i ${srcdir}/005-openjpeg2-include-dirs.patch
  patch -p1 -i ${srcdir}/static-mingw.patch
  patch -p1 -i ${srcdir}/backports.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  
  export CPPFLAGS="-DOPJ_STATIC -Dpoppler_cpp_EXPORTS" 
  export CFLAGS=$CPPFLAGS
  export CXXFLAGS=$CPPFLAGS
  unset PKG_CONFIG

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake.exe" -Wno-dev \
    -G"MSYS Makefiles" \
    -DBUILD_SHARED_LIBS=OFF \
    -DCMAKE_CXX_STANDARD_LIBRARIES="$(pkg-config --libs-only-l --static cairo libtiff-4) -lfreetype -lWinspool -lMsimg32 -lComdlg32" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DENABLE_XPDF_HEADERS=ON \
    -DENABLE_GTK_DOC=OFF \
    -DENABLE_GLIB=OFF \
    -DENABLE_QT5=OFF \
    -DENABLE_LIBCURL=OFF \
    -DBUILD_GTK_TESTS=OFF \
    -DENABLE_CMS=lcms2 \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DENABLE_UTILS=ON \
    ../${_realname}-${pkgver}

  CC="${MINGW_PREFIX}/bin/gcc.exe" \
  PKG_CONFIG_PATH="${MINGW_PREFIX}/lib/pkgconfig" \
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
