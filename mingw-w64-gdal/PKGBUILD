# Maintainer: Alexey Kasatkin <alexeikasatkin@gmail.com>
# ArchLinux maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Petri Moisio <petri.k.moisio@gmail.com>

_realname=gdal
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.2.1
pkgrel=1
pkgdesc="A translator library for raster geospatial data formats (mingw-w64)"
arch=('any')
url="https://gdal.org/"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-cfitsio"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-geos"
         "${MINGW_PACKAGE_PREFIX}-giflib"
         "${MINGW_PACKAGE_PREFIX}-hdf4"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-json-c"
         "${MINGW_PACKAGE_PREFIX}-libfreexl"
         "${MINGW_PACKAGE_PREFIX}-libgeotiff"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libmariadbclient"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libspatialite"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-netcdf"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-libpq"
         "${MINGW_PACKAGE_PREFIX}-proj"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-xz")
#optdepends=("${MINGW_PACKAGE_PREFIX}-postgresql")
options=('strip' 'staticlibs')
source=(https://download.osgeo.org/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('43d40ba940e3927e38f9e98062ff62f9fa993ceade82f26f16fab7e73edb572e')

prepare() {
  cd ${srcdir}
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  sed -i "s|/usr/local|${MINGW_PREFIX}|g" configure.ac
  sed -i "s|/usr|${MINGW_PREFIX}|g" configure.ac
  sed -i "s|mandir='\${prefix}/man'|mandir='\${prefix}/share/man'|g" configure.ac
  for p in m4/*.m4
  do
    sed -i "s|/usr|${MINGW_PREFIX}|g" $p
  done
  # bug: http://osgeo-org.1560.x6.nabble.com/gdal-dev-jpeg2000-jasper-error-compiling-gdal-2-1-from-git-release-branch-td5299100.html
  sed -i -e 's@uchar@unsigned char@' frmts/jpeg2000/jpeg2000_vsil_io.cpp

  # HDF4
  sed -i 's/-ldf/-lhdf -lxdr/g' configure.ac

  # GEOS
  sed -i 's/-lgeos_c/-lgeos_c -lgeos -lstdc++ -lm/g' m4/geos.m4

  # NetCDF
  sed -i 's/NCCONFIG --libs/NCCONFIG --libs --static | sed "s#-l-l#-l#g" /g' configure.ac
  #sed -i "s/-lnetcdf/(pkg-config --libs-only-l --static netcdf)/g"  configure.ac

  # LibTiff
  TIFFLIBS=$(pkg-config --libs-only-l --static libtiff-4)
  #sed -i "s/-ltiff/${TIFFLIBS}/g" configure.ac

  # PROJ
  PROJLIBS=$(pkg-config --libs-only-l --static proj)
  #sed -i "s/-lproj/${PROJLIBS}/g" configure.ac

  # Be careful: libcrypto.a and libxml2.a contain duplicate symbols
  SPATIALLIBS="-lspatialite -lxml2 -lproj -lgeos_c -lgeos $(pkg-config --libs-only-l --static freexl sqlite3 libtiff-4 libcurl)"
  #sed -i "s/-lspatialite/${SPATIALLIBS}/g" configure.ac

  ./autogen.sh

  sed -i "s/-lspatialite/${SPATIALLIBS}/g" configure
  sed -i "s/-lproj/${PROJLIBS}/g" configure
  sed -i "s/-ltiff/${TIFFLIBS}/g" configure
  sed -i "s/-lfreexl/-lfreexl -liconv/g" configure

}

build() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  export PKG_CONFIG="${MINGW_PREFIX}/bin/pkg-config --static"
  ##-DCURL_STATICLIB -DLIBXML_STATIC -DPCRE_STATIC -DOPJ_STATIC -DJAS_DLL=0
  export CPPFLAGS="-DPCRE_STATIC"
  CFLAGS+=" -fno-strict-aliasing"
  #LIBS="-lgeos_c -lgeos -lsqlite3 -lfreexl -lz -liconv -lstdc++ -lws2_32" \
  #GEOS_LIBS="-lgeos_c -lgeos -lstdc++ -lm" \
  PQ_LIBS="-lpq -lpgport -lpgcommon -lssl -lcrypto -lwsock32 -lsecur32 -lws2_32 -lgdi32 -lcrypt32 -lwldap32" \
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --disable-debug \
    --without-python \
    --without-perl \
    --without-threads \
    --without-libkml \
    --without-poppler \
    --without-xerces \
    --with-png \
    --with-liblzma \
    --with-webp="${MINGW_PREFIX}" \
    --with-spatialite="${MINGW_PREFIX}" \
    --with-expat-inc="${MINGW_PREFIX}/include" \
    --with-expat-lib="-L${MINGW_PREFIX}/lib -lexpat" \
    --with-jasper="${MINGW_PREFIX}" \
    --with-pg \
    --with-jpeg="${MINGW_PREFIX}" \
    --with-freexl="${MINGW_PREFIX}" \
    --with-proj="${MINGW_PREFIX}" \
    --with-geos="${MINGW_PREFIX}/bin/${MINGW_CHOST}-geos-config" \
    --with-openjpeg="${MINGW_PREFIX}" \
    --with-mysql="${MINGW_PREFIX}/bin/mariadb_config" \
    --with-netcdf="${MINGW_PREFIX}/bin/nc-config" \
    --with-hdf5="${MINGW_PREFIX}" \
    --with-hdf4="${MINGW_PREFIX}" \
    --with-netcdf="${MINGW_PREFIX}" \
    --with-crypto \
    --with-spatialite="yes" \
    --with-liblzma

  sed -i GDALmake.opt -e "s|EXE_DEP_LIBS.*|EXE_DEP_LIBS = \$\(GDAL_SLIB\)|g"
  sed -i GNUmakefile -e "s|\$(GDAL_ROOT)\/||g"

  # mariadb_config seems broken
  sed -i GDALmake.opt -e "s|-lmariadb|-lmariadbclient -lssl -lcrypto -lcrypt32 -lgdi32 -lz -lws2_32 -lshlwapi -pthread|g"

  # Print some output for debugging
  if grep -o "[^/]*\.a.*undefined reference to.*$" config.log; then
    echo "Found static linking errors in config.log. Drivers may have been disabled."
    exit 1
  fi

  if grep -o "[^/]*\.a.*multiple definition of.*$" config.log; then
    echo "Found symbol collisions config.log. Drivers may have been disabled."
    exit 1
  fi

  #read -n1 -r -p "Press any key to continue..." key
  make
  # make man
}

package () {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-man

  # install license
  install -D -m644 LICENSE.TXT "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  # Jeroen: do not ship gdal executables in the package because they are HUGE
  echo "Not including executables in rtools40 package:"
  rm -v ${pkgdir}${MINGW_PREFIX}/bin/*.exe
}
