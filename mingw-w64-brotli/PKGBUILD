# Maintainer: Will Jones <willjones127@gmail.com>

_realname=brotli
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.9
pkgrel=4
pkgdesc="Brotli compression library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/google/brotli'
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
license=('MIT')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/google/brotli/archive/v${pkgver}.tar.gz"
        '09b0992b6acb7faa6fd3b23f9bc036ea117230fc.patch'
        '9243cfaea9b9a2dde2199ee13e812cbbb942c98d.patch')
sha256sums=('f9e8d81d0405ba66d181529af42a3354f838c939095ff99930da6aa9cdf6fe46'
            '0b1c8045b4fa745e620b5c1a75377f9cda839fbb07c90bf3358b583866f063ee'
            'a5f4ad0e5f41e55a3d251a6201c4f92cd725425cdb1f2a0219646bc0f9ebbc88')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/09b0992b6acb7faa6fd3b23f9bc036ea117230fc.patch"
  patch -p1 -i "${srcdir}/9243cfaea9b9a2dde2199ee13e812cbbb942c98d.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      ../${_realname}-${pkgver}

  cmake --build .
}

check() {
  cd "${srcdir}"/build-${CARCH}

  cmake --build . --target test
}

package() {
  cd "${srcdir}"/build-${CARCH}

  DESTDIR="${pkgdir}" cmake --build . --target install

  install -D -m644 "${srcdir}"/brotli-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
