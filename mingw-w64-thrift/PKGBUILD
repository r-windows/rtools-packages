## Maintainer: Jeroen Ooms

_realname=thrift
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.13.0
pkgrel=1
pkgdesc="Framework for scalable cross-language services development (mingw-w64)"
arch=("any")
url="https://thrift.apache.org/"
license=("Apache-2.0")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "bison"
             "flex"
             "make")
options=("staticlibs" "strip" "!buildflags")
source=("https://archive.apache.org/dist/thrift/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=("7ad348b88033af46ce49148097afe354d513c1fca7c607b59c33ebb6064b5179")

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake . \
    -Wno-dev \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_CXX_STANDARD_LIBRARIES="$(pkg-config --libs --static openssl)" \
    -DWITH_SHARED_LIB="OFF" \
    -DBUILD_SHARED_LIBS="OFF" \
    -DCMAKE_BUILD_TYPE="Release" \
    -DBUILD_COMPILER="ON" \
    -DBUILD_TESTING="OFF" \
    -DBUILD_TUTORIALS="OFF" \
    -DWITH_LIBEVENT="OFF" \
    -DWITH_OPENSSL="ON" \
    -DOPENSSL_USE_STATIC_LIBS="ON" \
    -DWITH_ZLIB="ON" \
    -DWITH_QT5="OFF" \
    -DWITH_CPP="ON" \
    -DWITH_C_GLIB="OFF" \
    -DWITH_JAVA="OFF" \
    -DWITH_PYTHON="OFF" \
    -DWITH_HASKELL="OFF"
	
  make -j1
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" -j1 install
  ${pkgdir}${MINGW_PREFIX}/bin/thrift --version
}
