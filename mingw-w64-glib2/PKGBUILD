# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ignacio Casal Quinteiro <icq@gnome.org>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=glib2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.58.3
pkgrel=1
url="https://www.gtk.org/"
arch=('any')
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
license=(LGPL2)
# options=('debug' '!strip')
depends=("${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-python3"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("https://download.gnome.org/sources/glib/${pkgver%.*}/glib-${pkgver}.tar.xz"
        0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
        0001-win32-Make-the-static-build-work-with-MinGW-when-pos.patch
        0001-disable-some-tests-when-static.patch
        0001-Revert-tests-W32-ugly-fix-for-sscanf-format.patch
        vista.patch)
sha256sums=('8f43c31767e88a25da72b52a40f3301fefc49a665b56dc10ee7cc9565cbe7481'
            'ff0d3df5d57cf621cac79f5bea8bd175e6c18b3fbf7cdd02df38c1eab9f40ac3'
            '838abaeab8ca4978770222ef5f88c4b464545dd591b2d532c698caa875b46931'
            '0f44135a139e3951c4b5fa7d4628d75226e0666d891faf524777e1d1ec3b440b'
            '601b4da43aeccfa522ea46fcb9c33ec9530b8c4b965b8964abd3f4972b769cdd'
            'SKIP')

prepare() {
  cd "${srcdir}/glib-${pkgver}"

  patch -Np1 -i "${srcdir}"/0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
  patch -Np1 -i "${srcdir}"/0001-win32-Make-the-static-build-work-with-MinGW-when-pos.patch
  patch -Np1 -i "${srcdir}"/0001-disable-some-tests-when-static.patch
  patch -Np1 -i "${srcdir}"/0001-Revert-tests-W32-ugly-fix-for-sscanf-format.patch
  patch -Np1 -i "${srcdir}"/vista.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS//DTD"
  msg "Build static version"
  unset PKG_CONFIG
  mkdir -p ${srcdir}/build-${CARCH}-static && cd ${srcdir}/build-${CARCH}-static
  ../glib-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-shared \
    --enable-static \
    --disable-maintainer-mode \
    --disable-dependency-tracking \
    --disable-silent-rules \
    --disable-dtrace \
    --disable-libelf \
    --with-python=${MINGW_PREFIX}/bin/python3.exe \
    --with-threads=posix \
    --without-xml-catalog

  make
}

package() {
  cd "${srcdir}/build-${CARCH}-static"
  make DESTDIR="${pkgdir}" install
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/charset.alias
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.la

  install -Dm644 "${srcdir}/glib-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"

  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  pushd "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig" > /dev/null
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i gio-2.0.pc
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i glib-2.0.pc
  popd > /dev/null
}
